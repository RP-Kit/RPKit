/*
 * Copyright 2020 Ren Binden
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

def repoUserName = ""
def repoPassword = ""
def propertiesFile = new File("repo.properties")
if (propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    repoUserName = properties.getProperty("repo-username")
    repoPassword = properties.getProperty("repo-password")
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.4.21"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: "1.4.21"
    implementation group: "org.spigotmc", name: "spigot-api", version: "1.16.1-R0.1-SNAPSHOT"
    implementation group: "org.bstats", name: "bstats-bukkit", version: "1.7"
    implementation project(":rpk-core")
    implementation project(":bukkit:rpk-core-bukkit")
    implementation project(":bukkit:rpk-character-lib-bukkit")
    implementation project(":bukkit:rpk-economy-lib-bukkit")
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": version
    ]
}

apply plugin: "com.github.johnrengelman.shadow"

shadowJar {

    dependencies {
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8"))
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib"))
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib-common"))
        include(dependency("org.jetbrains:annotations"))
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk7"))
        include(dependency("org.jetbrains.kotlin:kotlin-reflect"))
        include(dependency("org.bstats:bstats-bukkit"))
    }

    relocate "kotlin", "com.rpkit.auctions.bukkit.shadow.lib.kotlin"
    relocate "org.jetbrains", "com.rpkit.auctions.bukkit.shadow.lib.org.jetbrains"
    relocate "org.intellij", "com.rpkit.auctions.bukkit.shadow.lib.org.intellij"
    relocate "org.bstats", "com.rpkit.auctions.bukkit.shadow.lib.org.bstats"
}

artifacts {
    archives shadowJar
}

uploadArchives {
    repositories.mavenDeployer {

        repository(url: "https://repo.rpkit.com/repository/maven-releases/") {
            authentication(userName: repoUserName, password: repoPassword)
        }

        pom.version = project(":bukkit:rpk-auction-lib-bukkit").version
        pom.artifactId = project(":bukkit:rpk-auction-lib-bukkit").name
        pom.groupId = project(":bukkit:rpk-auction-lib-bukkit").group

    }
}