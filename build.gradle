/*
 * Copyright 2020 Ren Binden
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
        classpath "com.github.breadmoirai:github-release:2.2.12"
    }
}

apply plugin: "com.github.breadmoirai.github-release"

subprojects {
    apply plugin: "kotlin"
    apply plugin: "maven"
    apply plugin: "org.jetbrains.dokka"
    group = "com.rpkit"
    version = "1.9.0-SNAPSHOT"

    repositories {
        mavenCentral()
        maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
        maven { url "https://ci.ender.zone/plugin/repository/everything/" }
        maven { url "http://repo.jenkins-ci.org/public/" }
        maven { url "https://maven.sk89q.com/repo/" }
        maven { url "https://repo.codemc.org/repository/maven-public" }
        maven { url "https://dl.bintray.com/xafero-solutions/xafero-source/" }
    }

    dependencies {
        testImplementation group: "io.kotlintest", name: "kotlintest-runner-junit5", version: "3.4.2"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}

def githubToken = ""
def githubPropertiesFile = new File("github.properties")
if (githubPropertiesFile.exists()) {
    def githubProperties = new Properties()
    githubProperties.load(new FileInputStream(githubPropertiesFile))
    githubToken = githubProperties.get("token")
}

githubRelease {
    authorization githubToken
    owner "RP-Kit"
    repo "RPKit"
    releaseName "Version " + version
    releaseAssets subprojects.stream()
            .flatMap { subproject ->
                subproject.evaluate()
                def noSource = true
                def sourceSets = subproject.sourceSets
                for (sourceSet in sourceSets) {
                    noSource = !sourceSet.allSource.isEmpty() || !noSource
                }
                if (!noSource) {
                    def files = subproject.tasks.jar.outputs.getFiles().getFiles().stream()
                    if (subproject.tasks.findByName("shadowJar")) {
                        files = subproject.tasks.shadowJar.outputs.getFiles().getFiles().stream()
                    }
                    files
                } else {
                    Arrays.stream(new File[0])
                }
            }.toArray { size -> new File[size] }
}

subprojects.forEach { subproject ->
    subproject.evaluate()
    if (subproject.tasks.findByName("shadowJar")) {
        project.tasks.githubRelease.dependsOn subproject.tasks.shadowJar
    } else {
        project.tasks.githubRelease.dependsOn subproject.tasks.jar
    }
}
